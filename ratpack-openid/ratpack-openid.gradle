/*
 * Copyright 2014 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

description = "Authentication via OpenID - http://ratpack.openid.net/"

apply from: "$rootDir/gradle/javaModule.gradle"

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'org.gebish:geb-gradle:0.9.2'
  }
}

import geb.gradle.saucelabs.SauceAccount

apply plugin: "geb-saucelabs"

repositories {
  maven { url "http://repository-saucelabs.forge.cloudbees.com/release" }
}

configurations {
  browserTestCompile.extendsFrom testCompile
  browserTestRuntime.extendsFrom testRuntime

  ["browserTestCompile", "browserTestRuntime"].each { configurationName ->
    getByName(configurationName).resolutionStrategy {
      force "org.apache.httpcomponents:httpclient:4.3.1", "org.apache.httpcomponents:httpmime:4.3.1"
      force "com.google.guava:guava:16.0"
      force "commons-logging:commons-logging:1.1.3"
    }
  }
}

sourceSets {
  browserTest {
    compileClasspath += (main.output + test.output)
    runtimeClasspath += (main.output + test.output)
  }
}

dependencies {
  compile project(":ratpack-session")
  compile("org.openid4java:openid4java:0.9.8") {
    exclude group: "com.google.inject", module: "guice"
    exclude group: "org.apache.httpcomponents", module: "httpclient"
  }
  testCompile project(":ratpack-groovy-test")
  testCompile commonDependencies.spock
  browserTestCompile "org.gebish:geb-spock:0.9.2"
  browserTestCompile "org.seleniumhq.selenium:selenium-firefox-driver:2.40.0"
  sauceConnect "com.saucelabs:sauce-connect:3.0.28"
}

task browserTest(type: Test) {
  testClassesDir = sourceSets.browserTest.output.classesDir
  classpath = sourceSets.browserTest.runtimeClasspath
}

sauceLabs {
  browsers {
    chrome_linux
  }
  task {
    testClassesDir = browserTest.testClassesDir
    classpath = browserTest.classpath

    systemProperty 'ratpack.port', '5050'
  }
  account {
    username = System.getenv(SauceAccount.USER_ENV_VAR)
    accessKey = System.getenv(SauceAccount.ACCESS_KEY_ENV_VAR)
  }
}

if (project.hasProperty("ciBuild")) {
  check.dependsOn allSauceTests
}

idea {
  module {
    testSourceDirs += sourceSets.browserTest.allSource.srcDirs
    scopes.TEST.plus += configurations.browserTestCompile
  }
}
